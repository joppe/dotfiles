#+title: fish functions vpn
#+property: header-args :tangle vpn.fish

* Table of contents :toc:
- [[#fish-vpn-function][Fish vpn function]]
  - [[#config][Config]]
  - [[#vpn-function][vpn function]]
  - [[#vpn-status-function][vpn-status function]]
  - [[#vpn-start-function][vpn-start function]]
  - [[#vpn-stop-function][vpn-stop function]]

* Fish vpn function

** Config
#+begin_src sh
set vpn_dir $HOME/.config/openconnect
#+end_src

** vpn function
The main function =vpn= accepts two arguments:
1. the command, it can be on of 'start', 'stop' or 'status'
2. the vpn config

#+begin_src sh
function vpn
    set -l cmd $argv[1]
    set -l ctx $argv[2]

    if test -z $cmd
        echo "No command specified." "Please use 'start','stop' or 'status'"
        return 1
    end
    
    if test $cmd != "status"; and test -z $ctx
        echo "No connection specified" "Please enter a connection after the command"
        return 1
    end

    switch $cmd
        case status
            vpn-status
        case start
            vpn-start $ctx
        case stop
            vpn-stop $ctx
        case '*'
            echo "Unknown command" $cmd "please use 'start','stop' or 'status'"
            return 1
    end
end
#+end_src

** vpn-status function
Show if there is a vpn connection active.

#+begin_src sh
function vpn-status
    set -l vpn_status (ip tuntap show)

    if string match -q "tun0: tun*" $vpn_status
        echo "‚¨ÜÔ∏èVPN is active"
    else
        echo "‚¨áÔ∏èVPN is off"
        sudo rm $vpn_dir/*.lock
    end
end
#+end_src

** vpn-start function
Start a vpn connection.

#+begin_src sh
function vpn-start
    set -l connection $argv[1]
    set -l url ""

    if test -e "$vpn_dir/$connection.lock"
        echo "VPN connection for $connection already established. Please run 'vpn stop $connection' first before starting"
        return 1
    else if test -e "$vpn_dir/$connection.cfg"
        sudo openconnect --background --config="$vpn_dir/$connection.cfg" --pid-file="$vpn_dir/$connection.lock" $url
        echo "üöÄ  VPN $connection started"
    else
        echo "No configuration found for $connection"
        return 1
    end

    return 0
end
#+end_src

** vpn-stop function
Stop a vpn function.

#+begin_src sh
function vpn-stop
    set -l connection $argv[1]
    set -l lockfile "$vpn_dir/$connection.lock"

    if test -e $lockfile
        set -l pid (head -1 $lockfile)

        sudo kill $pid
        echo "üõë  VPN $connection stopped"
    else
        echo "The VPN has not been started. Please start the vpn by running 'vpn start $connection'"
        return 1
    end
end
#+end_src
